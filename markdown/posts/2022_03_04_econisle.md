% Spelunking: Barski's EconIsle

Today I'm looking at Conrad Barski's EconIsle - a simple economic model.

* [Original Repo](https://github.com/drcode/EconIsle)
* [My Fork](https://github.com/RedPenguin101/EconIsle)
* [Conrad's video](https://www.youtube.com/watch?v=Rx_URujI3pw)

The premise is that there is a Stone Age island with 40 people in it. Each person gathers a resources (Mammoth, Ketchup, Lettuce, Bread), and trades for that resource in a marketplace, aiming to get enough of each to make Mammoth Burgers. The program tracks the price of each resource and the number of burgers made each day.

# The economic model, or engine
I think the first thing to look at is the `engine` namespace. This contains the actual economic model

```clojure
(ns elon-tusk.engine
  #?(:cljs (:require-macros [snek.core :refer [defsnek defn]]
                            [fbc-utils.core :refer [forv]]))
  (:refer-clojure :rename {defn      core-defn
                           defmethod core-defmethod})
  (:require [fbc-utils.core :as ut]
            [fbc-utils.debug :as db]
            [clojure.pprint :as pp]
            [snek.core :as sn]
            #?(:clj [snek.core :refer [defsnek defn]])
            #?(:clj [fbc-utils.core :refer [forv]])))
```

* `fbc-utils` and `snek` are Conrad's own libraries
* `fbc-utils` seems to be a lot of mostly mathematical functions for both JVM and JS implementations: `degrees->rad`, `acos`, etc.
* `snek` seems like a sort of static typing/validation library. Lots of macros for rewriting and checking. Might try and pull this out, seems confusing for my purposes.

The require after removing `snek` and `forv`:

```clojure
(ns elon-tusk.engine
  (:require [fbc-utils.core :as ut]
            [fbc-utils.debug :as db]
            [snek.core :as sn]
            [clojure.pprint :as pp]))
```

(`sn/modify` is left in, didn't seem to be a good way to remove it).

Foods are defined like this:

```clojure
(def foods {:mammoth {:id    :mammoth
                      :ease  4
                      :order 0}
            :ketchup {:id    :ketchup
                      :ease  8
                      :order 1}
            :lettuce {:id    :lettuce
                      :ease  6
                      :order 2}
            :bread   {:id    :bread
                      :ease  6
                      :order 3}})
```

Ease is presumably how easy each food is to collect. Order is an increasing integer, presumably something to do with the order in which the collection actions are fired.

Next are some words:

```clojure
(def stonage-word-1 [:saber :rock :arrow :mammoth :fur :sling])
(def stonage-word-2 [:skull :head :chest :foot :nose :arms])
(def stonage-full-names [:elon-tusk :king-fred :yakyak :ogg :biggo :berf :snuud :yikyik :rokko :yukko])
```

These just seem to be used to generate the names of the people on the island.

These people are defined as 'agents', and created with 

```clojure
(defn gen-agent []
  {:money money-supply-per-agent}) 
;; money supply is set to 1000, and the number of agents is 40
```

Init state...creates the initial state. It has 4 keys:

* **agents** (people on the island), each having an id 0-39
* **foods**, which contains the price, order-cur, and order-num, whatever they are.
* **agent-foods**, which seems to be a map where keys are a tuple of `(agent-id, food)`. Each agent-food has a productivity score, which is randomly rolled based on 'ease' in the food definition
* **orders**, an empty map

```clojure
(comment
  ((:agents (init-state)) 5) ; {:money 1000, :name "berf", :id 5}
  (first (:agent-foods (init-state))) ; [[39 :lettuce] {:id [39 :lettuce], :productivity 2, :owned 0}]
  (:ketchup (:foods (init-state))) ; {:id :ketchup, :price 150, :order-cur 0, :order-num 0}
  )
```

`target-formula` is a long equation, which takes 4 numbers and returns another number.

$$
c = \text{cost others}
\newline
a = \text{max amount}
\newline
m = \text{money}
\newline
p = \text{max price}
\newline
\text{}
\newline
t = \frac{\sqrt{p^2 + 4acp + 4cm} - p}{2c}
$$

This is used in `target-burgers-optimum-formula`, which has a helpful comment: "How many burgers can the agent expect to make, given owned food & money, assuming agent doesn't buy their most abundant food."

This function is passed the model state and an agent id.

**NOTE from later:** optimum formula is totally not used anywhere.

In addition there is a `target-burgers-as-much-as-affordable` which seems to do the same thing in a different way.

Next we have some pretty gnarly functions for `work`, `eat`, and `sleep`, culminating in:

```clojure
(defn turn [state options]
  (-> state
      (work options)
      eat
      (sleep options)))
```

This is clearly an implementation of the main game loop.

Next you have a couple of helper functions `money-total` and `food-total`.

And finally a `main`

```clojure
(defn main []
  (let [k (iterate (fn [state]
                     (let [state (turn state {})
                           log (purge-log)]
                       (println "burgers " (apply + (keep :burgers log)))
                       state))
                   (init-state))
        states (take 60 k)]
    (doseq [state states]
    (pp/pprint (sn/query {:foods {:_ {:id     nil
                                      :price nil}}}
                         (last states)))
    nil))
```

Which simulates the first 60 turns, printing the number of burgers produced at each turn.

So in summary, the NS has the following parts:

1. Logging helpers
2. Config
3. State Initialization
4. Burger making calculations
5. Model loop

The meat (no pun intended) is clearly in the last two.

Let's create a couple of helpers so we can look at the state:

```clojure
(def turn-no-opt #(turn % {}))
(defn main2 []
  (take 10 (iterate turn-no-opt (init-state))))

(last (main2))
{:agents
 {0 {:money 1337.3949335957013, :name "elon-tusk", :id 0},,,}

 :agent-foods
 {[39 :lettuce] {:id [39 :lettuce], :productivity 7, :owned 0}} ,,,
 
 :orders {},
 :foods
 {:mammoth
  {:id :mammoth,
   :order-cur 0,
   :order-num 0,
   :price 210.53740073994146},
  :ketchup
  {:id :ketchup,
   :order-cur 0,
   :order-num 0,
   :price 104.48871792802733},,,}}
```

Seems simple enough, but the devil will be in the implementation. Let's look at the first two states instead: `(take 2 (main2))`

It seems like 'owned' goes down to zero at the end of each turn, and all 'orders' get filled (since orders remain empty). Lets slice the lettuce a bit finer, and examine the impact on state of the individual turn steps:

First define some helpers to summarise the state. We'll look at a single agent, 6:

```clojure
(defn get-agents-foods [state agent-id]
  (into {} (filter (fn [[[k]]] (= agent-id k)) (:agent-foods state))))

(defn get-agents-orders [state lookup-agent-id]
  (filter (fn [{:keys [id agent-id]}]
            (or (= lookup-agent-id (second (id)))
                (= lookup-agent-id agent-id))) 
          (vals (:orders state))))

(defn summarize-agent-6 [state]
  {:agent (get-in state [:agents 6])
   :agent-food (get-agents-foods state 6)
   :orders (get-agents-orders state 6)})
```

Next we simulate 10 turns to give ourselves a baseline, then do one 'work' step, and examine the states at both points:

```clojure
(def ten-turns (last (take 10 (iterate turn-no-opt (init-state)))))
(def after-working (work ten-turns {}))

(summarize-agent-6 ten-turns)
{:agent {:money 1151.994712556641, :name "snuud", :id 6},
 :agent-food
 {[6 :bread] {:id [6 :bread], :productivity 6, :owned 0},
  [6 :lettuce] {:id [6 :lettuce], :productivity 0, :owned 0},
  [6 :ketchup] {:id [6 :ketchup], :productivity 11, :owned 0},
  [6 :mammoth] {:id [6 :mammoth], :productivity 0, :owned 0}},
 :orders ()}

(summarize-agent-6 after-working)
{:agent {:money 1151.994712556641, :name "snuud", :id 6},
 :agent-food
 {[6 :bread] {:id [6 :bread], :productivity 6, :owned 0},
  [6 :lettuce] {:id [6 :lettuce], :productivity 0, :owned 0},
  [6 :ketchup] {:id [6 :ketchup], :productivity 11, :owned 11},
  [6 :mammoth] {:id [6 :mammoth], :productivity 0, :owned 0}},
 :orders
 ({:id [:lettuce 6], :agent-id 5, :amount 6}
  {:id [:bread 6], :agent-id 15, :amount 7}
  {:id [:ketchup 0], :agent-id 6, :amount 9}
  {:id [:mammoth 6], :agent-id 24, :amount 2})}
```

We can see that agent 6's ketchup amount has gone up by 11. Presumably their work involved gathering ketchup. There are also some orders. From what they are we can infer that agent 6 has ordered 6 lettuce from agent 5, 7 bread from agent 15, and 2 mammoth from agent 24. Agent 0 has ordered 9 ketchup from agent 6.

**NOTE from later:** This is incorrect. Looking into the work function more closely: the syntax for orders is `{:id [food ORDER_NUMBER] :agent_id id-of-agent-PLACING-order :amount amount}`, where order number is just a unique (to that food type) identifier for the order, not a reference to an agent.

So the work phase clearly adds to the amount of an agents resources and decides what orders to place.

Next, after eating and after sleeping.

```clojure
(def after-eating (eat after-working))
(summarize-agent-6 after-eating)
{:agent {:money 1117.4909533834962, :name "snuud", :id 6},
 :agent-food
 {[6 :bread] {:id [6 :bread], :productivity 6, :owned 0},
  [6 :lettuce] {:id [6 :lettuce], :productivity 0, :owned 0},
  [6 :ketchup] {:id [6 :ketchup], :productivity 11, :owned 0},
  [6 :mammoth] {:id [6 :mammoth], :productivity 0, :owned 0}},
 :orders ()}

(def after-sleeping (sleep after-eating {}))
(summarize-agent-6 after-sleeping)
{:agent {:money 1117.4909533834962, :name "snuud", :id 6},
 :agent-food
 {[6 :bread] {:id [6 :bread], :productivity 6, :owned 0},
  [6 :lettuce] {:id [6 :lettuce], :productivity 0, :owned 0},
  [6 :ketchup] {:id [6 :ketchup], :productivity 11, :owned 0},
  [6 :mammoth] {:id [6 :mammoth], :productivity 0, :owned 0}},
 :orders ()}
```

After eating, agent 6's money has gone down, they own nothing again, and there are no orders. Presumably the eat phase fills the order, has the agent make and eat the burgers.

Sleeping doesn't seem to change the state at all. But a glance at the `sleep` function shows that this is where prices are recalculated (see below code snips showing foods before and after sleeping), and any 'surplus' owned foods are set to 0. Interesting that the simulation doesn't allow the creation of surplus. I wonder what that would do.

```clojure
(:foods after-eating)
 {:mammoth
  {:id :mammoth,
   :order-cur 14,
   :order-num 14,
   :price 210.53740073994146},
  :ketchup
  {:id :ketchup, :order-cur 5, :order-num 5, :price 94.53741145869138},
  :lettuce
  {:id :lettuce,
   :order-cur 8,
   :order-num 11,
   :price 127.64411248271482},
  :bread
  {:id :bread, :order-cur 8, :order-num 10, :price 104.48871792802733}}
(:foods after-sleeping)
 {:mammoth
  {:id :mammoth, :order-cur 0, :order-num 0, :price 221.06427077693854},
  :ketchup
  {:id :ketchup, :order-cur 0, :order-num 0, :price 99.26428203162595},
  :lettuce
  {:id :lettuce, :order-cur 0, :order-num 0, :price 121.26190685857907},
  :bread
  {:id :bread, :order-cur 0, :order-num 0, :price 99.26428203162595}}
```

OK, I think that's the basics. The questions that came up during that run:

* How are agents deciding what resource to collect during the eat phase?
* How are they deciding which orders to place, and with whom?
* How are they deciding how much of what they collected can go to market?
* In `:foods` what do `:order-cur` and `order-num` mean? In some cases they are the same, in some cases different (though order-num seems to always be >= order-cur)
* How does the price of the foods change?
* What happens when an agent runs out of money?

## Work

A lot of these get answered in the "Work" stage. So let's look at that.

At the top level, `work` is a reduction over `state` and the agents, with the reduction function being this

```clojure
(let [[food produced] ;; snipped for concision
      owned           (get-in acc [:agent-foods [id food] :owned])
      amount          (+ owned produced)
      acc             (assoc-in acc [:agent-foods [id food] :owned] amount)
      burgers         (target-burgers-as-much-as-affordable acc id)
      order-num       (get-in acc [:foods food :order-num])]
  (log {:cmd      :work
        :agent-id id
        :food     food
        :produced produced})
  (-> acc
      (assoc-in [:orders [food order-num]]
                {:id       [food order-num]
                 :agent-id id
                 :amount   (max 0 (- amount burgers))})
      (update-in [:foods food :order-num] inc)))
```

`[food produced]` just gets the most valuable thing each agent can collect (productive * price) and collects as much as they can. Then the accumulator (`:agent-foods`) is updated with with newly produced food.

`burgers` is calculated by `target-burgers-as-much-as-affordable` (which I'll look at in a bit). Then an order is placed for the surplus of the food they produced (i.e. that won't be used in making burgers). This changes how I thought orders worked. I'd assumed that the agent placed an order for things they wanted. In fact it's like they are 'bringing things to market' and putting them out on stalls, so others can buy them. Orders is kind of a funny name for it. Maybe 'marketplace availability' or something.

The only other thing being done here is handling the incrementing of the unique identifier for the order.

OK, so `target-burgers...`.

```clojure
(defn calc-affordability [ownership money]
  (let [min-food-owned (:owned (apply min-key :owned ownership))]
    (loop [min-food-owned min-food-owned
           money          money]
      (let [next-min-food-owned (when-let [k (seq (filter (fn [{:keys [owned] :as food}]
                                                            (< min-food-owned owned))
                                                          ownership))]
                                  (apply min (map :owned k)))
            cost                (apply +
                                       (map :price
                                            (filter (fn [{:keys [owned] :as food}]
                                                      (>= min-food-owned owned))
                                                    ownership)))
            max-units           (int (/ money cost))
            max-level           (+ min-food-owned max-units)]
        (if (or (not next-min-food-owned) (>= next-min-food-owned max-level))
          max-level
          (recur next-min-food-owned (- money (* (- next-min-food-owned min-food-owned) cost))))))))

(defn target-burgers-as-much-as-affordable
  [{:keys [foods]
    :as   state}
   agent-id]
  (let [ownership (for [food-id (keys foods)]
                    {:food-id food-id
                     :owned   (get-in state [:agent-foods [agent-id food-id] :owned])
                     :price   (get-in state [:foods food-id :price])})
        money     (get-in state [:agents agent-id :money])]
    (calc-affordability ownership money)))
```

Yikes!

So `target` is passed the state and agent id. It creates an 'ownership' maps, with how much the agent owns of each food type, and gets the amount of money they have. Then both of these are passed to `calculate`. Which, gotta be honest, not going to dissect it. It's pretty clear what it does: Given a) the food you own, b) the money you have and c) the price of each food on the market, what is the maximum number of burgers you can make? The additional question I have is "do they factor in the selling of their surplus into the calculation of the number of burgers?"

So let's see how many questions I've answered:

* ***How are agents deciding what resource to collect during the eat phase?***
* They decide based on what would make them the most money if they were to sell on the marketplace.
* ***How are they deciding which orders to place, and with whom?***
* They are not! At least not here. Instead, they are taking their surplus food to the marketplace and 'offering' it to any buyers, like they're setting out a stall.
* ***How are they deciding how much of what they collected can go to market?***
* They figure out the maximum number of burgers they can make, and anything they own that is not required to make those burgers, they sell.
* ***In `:foods` what do `:order-cur` and `order-num` mean? In some cases they are the same, in some cases different (though order-num seems to always be >= order-cur)***
* `order-num` is a unique and monotonically increasing identifier for the order. I still don't know what `order-cur` is. I could probably guess, but will probably come across it in `eat`

## Eat

Is a monster, about 50 lines, nested reduces, there's a _very_ deep `recur` in there. I'm pretty much going to guess what it does.

* The top level is a reduce which accumulates the state, and iterates over the agents.
* The first step for each agent is to calculate the max number of burgers again
* For each food, figure out how many you need to make those burgers and then run through every order (in the order they were placed - that's what 'order-cur' is doing I think), buying them until you have enough. This will decrease the buying agents money and increase the seller's money. Vice versa for the amount of the food in each agents pocket.

* ***What happens when an agent runs out of money?*** Nothing, I don't think - there doesn't seem to be a check on whether the buyer has enough money. I guess it's limited by the 'calculate burgers' formula.

## Sleep

This one is more manageable. First, all food inventories are set to zero (I guess they rot overnight). Orders are also zeroed. If there are surplus orders in the marketplace (i.e. there were orders that didn't sell out), the price is multiplied by 0.95. Otherwise by 1.05. This is another place `order-cur` is used.

* ***In `:foods` what does `:order-cur` mean?*** Basically "earliest un-bought order" It's used so that, when a buyer is trying to buy, they will buy from the earliest one. It's also used to check if there is a surplus of the food in the marketplace, so the price can be adjusted.
* ***How does the price of the foods change?*** If there is a surplus of a food in the marketplace, the price is 95%, otherwise it's 105%.

## Back to the burger calculation

I'm still a bit in the dark how they calculate the number of burgers they want to produce.

To get some insight, I threw in a quick `debug` to capture the inputs:

```clojure
(defn calc-affordability [ownership money]
  (def debug {:ownership ownership :money money})
  (let [min-food-owned (:owned (apply min-key :owned ownership))]
    ,,,))

debug
;; => {:ownership
;;     ({:food-id :mammoth, :owned 6, :price 210.53740073994146}
;;      {:food-id :ketchup, :owned 0, :price 94.53741145869138}
;;      {:food-id :lettuce, :owned 0, :price 141.08033484931636}
;;      {:food-id :bread, :owned 0, :price 127.64411248271485}),

(calc-affordability (:ownership debug) (:money debug))
;; => 3;     :money 1114.1311429257821}
```
So the mathematical problem is: given the prices, what is the maximum number of burgers that can be made? (I think you need one of each to make a burger).

So the problem is to maximize $a$ in the formula

$$
a(p_m + p_k + p_l + p_b) \le m
\newline
a p \le m
\newline
a \le \frac{m}{p}
$$

We're solving for $a$ (amount of burgers) where $m$ is (adjusted) money and $p$ is the total price of the burger (the sum of the individual prices).

Note: to simplify this, 'sell' all of your ownership and add the proceeds to your money.

A couple of quick calcs:

```clojure
(calc-affordability (:ownership debug) (:money debug))
;; Conrads calc
;; => 3

(defn my-affordability [ownership money]
  (let [prices (into {} (map (fn [m] [(:food-id m) (:price m)]) ownership))
        owned-resources (into {} (map (fn [m] [(:food-id m) (:owned m)]) ownership))
        adjusted-money (+ money (apply + (vals (merge-with * prices owned-resources))))
        burger-price (apply + (vals prices))]
    (int (/ adjusted-money burger-price))))

(my-affordability (:ownership debug) (:money debug))
;; => 4
```

So clearly:

* ***Do they factor in the selling of their surplus into the calculation of the number of burgers?***
* No. Presumably the agent is being conservative, assuming they won't be able to sell their surplus. I think it would also make the 'clearing' of transactions in the market much more complicated!

OK, that's probably enough on the `engine` namespace. But before moving on I'll take a moment to describe the economic model concisely, and in general terms:

## Summary of and thoughts on the economic model
The parts of the model are:

* **Agents**: individuals who have 
    * _desires_, some goal, some gauge of 'satisfaction' or 'happiness' - here expressed in number of burgers, a view not dissimilar to my own. 
    * have the ability to _produce_, with differing capabilities, resources, and in effect act to maximize their productivity.
    * _possessions_, though only of inventory, not of capital, and with the exception of money, the current simulation doesn't allow for creation of surplus.
    * The ability to _trade_ on...
* A **Marketplace** where goods can be exchanged, and a price mechanism for adjusting prices in response to supply and demand. It works by sellers placing 'sell' orders (which all have the same price) and buyers meeting them.
* A **Currency**, or medium of exchange, which avoids barter economy

This is clearly a very simplistic and pedagogical model. In his video, Conrad gives two examples of the 'scenarios' that can be examined: centralized price controls, and the impact of productivity improvements on jobs. This is done via the 'options' map that has popped up a couple of times.

We can see the outcome of this by running the simulations:

```clojure
(defn run [options]
  (->> (init-state)
       (iterate (fn [state]
                  (let [state (turn state options) log (purge-log)]
                    (update state :total-burgers conj (apply + (keep :burgers log))))))
       (take 60)
       (last)))

(let [outcome (run {})]
  {:burgers (apply + (:total-burgers outcome))
   :mammoth-price (get-in outcome [:foods :mammoth :price])})
;; => {:burgers 3417, :mammoth-price 267.0231698987345}

(let [outcome (run {:mammoth-price 100})]
  {:burgers (apply + (:total-burgers outcome))
   :mammoth-price (get-in outcome [:foods :mammoth :price])})
;; => {:burgers 661, :mammoth-price 100}

(let [outcome (run {:vacuum true})]
  {:burgers (apply + (:total-burgers outcome))
   :mammoth-price (get-in outcome [:foods :mammoth :price])})
;; => {:burgers 4073, :mammoth-price 241.59239181314067}
```

The outcome is clear: price controls cause overall 'happiness' (burgers) to go down. Technology improvements which increase productivity make happiness go up. (Remember, this is a very simple model, and no real-world conclusions should be drawn from this!)

Conrad also says the model can be used to illustrate the market mechanism (supply and demand) and comparative advantage.

# Other namespaces
To be honest, most of the other NS are web rendering using qlkit, a React web framework inspired by OmNext, and using graph queries for state management. Sounds interesting, but seems too alpha to dig into at this point.
