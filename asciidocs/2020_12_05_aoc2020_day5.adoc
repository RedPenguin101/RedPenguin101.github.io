= Advent of Code 2020: Day 5

https://adventofcode.com/2020/day/5[Problem link]

https://github.com/RedPenguin101/aoc2020/blob/main/day5.clj[Solution in github]

== Problem summary

Given a boarding pass in the form `FBFBBFFRLR`, calculate what row and column of the plane this represents, then calculate the seat id (8*row + col).

Part 1, find the highest seat id. Part 2, find your seat (it's the only one that's empty). 

== Problem decomposition

The boarding pass has 2 sections: the first 7 characters give you the row, and the last 3 give you the column, using _binary space partitioning_.

That is, if there are 7 columns, numbered from left to right, 0-7, a string of "RLR" will represent

. R: the seat is in one of columns 4, 5, 6, 7.
. L: the seat is in one of columns 4, 5.
. R: the seat is in column 5.

Another way to think of this is that each position in the input represents a number of 2^n. For rows:

* 1st: 64
* 2nd: 32
* 3rd: 16
* 4th: 8
* 5th: 4
* 6th: 2
* 7th: 1

If the letter in that position is a "B", then take the number. Otherwise take 0. Then add all the numbers together.

So "FBFBBFF": [0 32 0 8 4 0 0] = 44.

This gives us all we need to solve the problem.

For part 2, we want to find the 'missing' value from a sequence. In other words, we want the difference between our sequence and the complete set of numbers between x and y. We can use set logic for that. 

== The solution

The form of a function getting from the (sub)string to a row or column number is:

. associate each character with it's position value
. filter on any B or R character (so ignoring all the zero values)
. add up all the remaining values.

Since row and column have the same format, we can use a single template HOF for both.

[source,clojure]
----
(letfn [(f [v char]
          (fn [string] (->> string
                            (map vector v)
                            (filter (comp #{char} second))
                            (map first)
                            (apply +))))]
  (def row (f [64 32 16 8 4 2 1] \B))
  (def col (f [4 2 1] \R)))
----

The seat ID calculation and solution to part 1 is trivial

[source,clojure]
----
(defn seat-id [string]
  (+ (* 8 (row (subs string 0 7)))
     (col (subs string 7))))

(apply max (for [boarding-pass (clojure.string/split-lines (slurp "resources/day5input"))]
             (seat-id boarding-pass)))
----

As mentioned, for part 2 we use some simple set logic to find the difference between the seat ids of our boarding passes and the complete set of numbers between the smallest and largest boarding passes we can see.

[source,clojure]
----
 (let [taken-seats (sort (map seat-id (split-lines (slurp "resources/day5input"))))]
    (difference (set (range (first taken-seats) (last taken-seats)))
                (set taken-seats)))
----

== Thoughts, lessons learned

None, other than Clojure is a very beautiful language for solving this problem, simple though it was. It feels like I just told the computer exactly what I wanted to do and it did it.

I used a `letfn` with indented `def` to define the row and col functions. Nested defs are generally discouraged, though I think it's OK in this case.

== Full Solution

[source,clojure]
----
(letfn [(f [v char]
          (fn [string] (->> string
                            (map vector v)
                            (filter (comp #{char} second))
                            (map first)
                            (apply +))))]
  (def row (f [64 32 16 8 4 2 1] \B))
  (def col (f [4 2 1] \R)))

(defn seat-id [string]
  (+ (* 8 (row (subs string 0 7)))
     (col (subs string 7))))

(comment
  "Part 1"
  (apply max (for [boarding-pass (clojure.string/split-lines (slurp "resources/day5input"))]
               (seat-id boarding-pass)))
  ;; => 885

  "Part 2"
  (let [taken-seats (sort (map seat-id (split-lines (slurp "resources/day5input"))))]
    (difference (set (range (first taken-seats) (last taken-seats)))
                (set taken-seats)))
  ;; => #{623}
  )
----

== Other solutions

* https://github.com/tschady/advent-of-code/blob/master/src/aoc/2020/d05.clj[tschady]
* https://github.com/jreighley/aoc2020/blob/master/src/day5.clj[jreighley]
* https://github.com/lambdaisland/aoc_2020/blob/main/src/lambdaisland/aoc_2020/puzzle05.clj[lambdaisland]
* https://github.com/ocisly/advent2020/blob/94b52d8bb63865a8ace75f34226f6acb390c69ef/day-5.clj[ocisly]
* https://github.com/rjray/advent-2020-clojure/blob/master/src/advent_of_code/day05bis.clj[rjray]
* https://github.com/zelark/AoC-2020/blob/3cc64ff25278f82d262c4c32c2eec4268c0997a5/src/zelark/aoc_2020/day_05.clj[zelark]
* https://github.com/green-coder/advent-of-code-2020/blob/master/src/aoc/day_5.clj[green-coder]
* https://github.com/nbardiuk/adventofcode/blob/master/2020/src/day05.clj[nbardiuk]
* https://github.com/transducer/adventofcode/blob/master/src/adventofcode/2020/day5.clj[transducer]

There were a few tricks here around binary parsing that I didn't know.

For example from `tschady`:

[source,clojure]
----
(defn seat-id [seat-code]
  (-> seat-code
      (str/escape {\B 1, \F 0, \R 1, \L 0})
      (Integer/parseInt 2)))
----

You use `parseInt` to parse a string like "10101010", with the optional 3rd arg `2` (binary), and it will return 170 (the decimal representation of 10101010).

Because the seat-id formula is `8*r + c`, you can skip the step of calculating rows and columns independently, and apply this to the 'full' input string. Instead of using the mapping of `[64 32 16 ,,,]` for row and `[4 2 1]` for col, you can just use `[512 256 128 64 ,,, 4 2 1]`. Very neat!

Also interesting in the above is the use of https://clojuredocs.org/clojure.string/escape[`escape`]. I've not seen this before, but it looks like it's basically a 'replace'. You pass in a string, and a map of char->string (or char->something), with all the instances of the chars being replaced.

`lambdaisland` used some bit-shifting, which frankly I don't get, but I _think_ does the same thing. I don't use Clojure for shifting bits around manually!

[source,clojure]
----
(defn bits->num [bits]
  (reduce (fn [num bit]
            (-> num
                (bit-shift-left 1)
                (bit-or bit)))
          0
          (map chars bits)))
----

`ocisly` and a couple of others had an interesting approach for finding the position

[source,clojure]
----
(defn find-pos [low high [x & xs]]
  (if (= low high) high
    (let [mid (+ low (/ (- high low) 2))]
      (cond
        (#{\L \F} x) (recur low (int (Math/floor mid)) xs)
        (#{\R \B} x) (recur (int (Math/ceil mid)) high xs)))))

(def find-row (partial find-pos 0 127))
(def find-col (partial find-pos 0 7))
----

The algorithm here is:

. Provide a low-high range and a sequence of characters
. if low=high, return high (termination condition)
. otherwise, calculate the midpoint of high and low
. then if first value of the sequence is L or F, recur with low-(floor mid), and the tail of the sequence
. if the first value is R or B, recur with (ceil mid)-high and the tail of the sequence

So for "FBFBBFF":

. low-high is 0-127
. first loop: midpoint is 63.5, char is F, so recur with 0-63
. second loop: midpoint is 31.5, char is B, so recur with 32-63
. third loop: midpoint is 47.5, char is F, so recur with 32-47
. forth loop: midpoint is 39.5, char is B, so recur with 40-47
. fifth loop: midpoint is 43.5, char is B, so recur with 44-47
. sixth loop: midpoint is 45.5, char is F, so recur with 44-45
. seventh loop: midpoint is 44.5, char is F, so recur with 44-44
. low=high, so return 44

This is a very literal description of the process described in the problem statement, but I feel like it's rather more low level than it needs to be.

`green-coder` used `edn/read-string` to do the binary conversion.

[source,clojure]
----
(->> seating
     (drop 7)
     (take 3)
     (map {\L \0 \R \1})
     (apply str "2r")
     (edn/read-string))
----

`(->> "FBFBBFFRLR" (drop 7) (take 3) (map {\L \0 \R \1}) (apply str "2r"))` returns `2r101`.

`(edn/read-string "2r101")` returns 5.

I _think_ that "2r101" is interpreted by edn as 'a two radix (i.e. binary) representation of an integer'.

For part 2, `lamdaisland` also used the other way people used to find the missing seat. Rather than `difference`, he partitioned the sorted seats with a sliding 2-length window, and tested whether the right value was 2 greater than the left value.

[source,clojure]
----
(some (fn [[l h]]
        (when (= h (+ 2 l))
          (inc l)))
      (partition-all 2 1 (sort seat-ids)))
----

The other way I saw of doing this was `nbardiuk`:

[source,clojure]
----
(let  [ids (->> input ids set)]
    (->> (iterate inc (apply min ids))
         (drop-while ids)
         first))
----

. start with a set of the seat-ids you have
. create an infinite sequence of integers from the minimum id.
. from that infinite sequence, drop all of those values while you recognise the id
. return the first id in that sequence (i.e. the first one you don't recognise)
